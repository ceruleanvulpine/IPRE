{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "**STACK INSTRUCTIONS** \n",
    "- R6 will store \"going\" addresses while going to push/pop\n",
    "- R7 will store \"return\" addresses while going to push/pop\n",
    "- R5 will be used in push/pop to point to top of stack, so won't be saved\n",
    "\n",
    "To call a subroutine: \n",
    "-     LEA R6, [SUBROUTINE LABEL]\n",
    "-     JSR PUSH\n",
    "\n",
    "Before returning from a subroutine: \n",
    "-     LEA R0, POP\n",
    "-     JMP R0\n",
    "\n",
    "Path: \n",
    "- Store address of subroutine in R6\n",
    "- JSR to push, which stores starting point in R7\n",
    "- Push does JMP R6 to get to subroutine\n",
    "- Subroutine JMPs to pop\n",
    "- Pop RETs to starting point \n",
    "\n",
    "Eventually, we may need to alter some of these instructions as things get too far apart for JSR or w/e."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "**ROOM/STATE DATA FORMAT**\n",
    "\n",
    "(subject to changes?)\n",
    "\n",
    "Every room/state stored in memory has a label pointing to the beginning of its data block, and variables stored at regular distances relative to that top. \n",
    "\n",
    "For all rooms:\n",
    "- Label + 0: which quality (HP/stats) the room changes (0 for none) [Note: Can this field also be used to change variables, e.g. whether you have a sword?]\n",
    "- Label + 1: amount of quality change\n",
    "- Label + 2: type of state (0 for user-input directed, 1 for dice-directed, 2 for variable-directed)\n",
    "\n",
    "Then if user-input room: \n",
    "- Label + 3, 4, 5: addresses of destinations for 1, 2, and 3 inputs\n",
    "- Label + 6: Description string in compressed format\n",
    "\n",
    "Or if dice room: \n",
    "- Label + 3: Stat to check\n",
    "- Label + 4: Difficulty of check\n",
    "- Label + 5: Destination if success\n",
    "- Label + 6: Destination if failure\n",
    "\n",
    "Or if variable room:\n",
    "- Label + 3: Address of variable to check \n",
    "- Label + 4: Value of that variable that counts as a success\n",
    "- Label + 5: Destination if success\n",
    "- Label + 6: Destination if failure"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "**STAT CODES**\n",
    "1. HP\n",
    "2. CHA\n",
    "3. VIT\n",
    "4. WIS"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Assembled! Use %dis or %dump to examine; use %exe to run.\n"
     ]
    }
   ],
   "source": [
    ".ORIG x3000\n",
    "BRnzp LOOP_START\n",
    "\n",
    ";;-----------------------------------------------------------------\n",
    ";; Setup for the stack \n",
    "Top:    .FILL Stack\n",
    "Stack:  .BLKW #100\n",
    "\n",
    "PUSH:   LD R5, Top\n",
    "        STR R0, R5, #0      ;; Save R0 to Stack\n",
    "        ADD R5, R5, #1      ;; Increment Stack\n",
    "        STR R1, R5, #0      ;; Save R1 to Stack\n",
    "        ADD R5, R5, #1      ;; Increment Stack\n",
    "        STR R2, R5, #0      ;; Save R2 to Stack\n",
    "        ADD R5, R5, #1      ;; Increment Stack\n",
    "        STR R3, R5, #0      ;; Save R3 to Stack\n",
    "        ADD R5, R5, #1      ;; Increment Stack\n",
    "        STR R4, R5, #0      ;; Save R4 to Stack\n",
    "        ADD R5, R5, #1      ;; Increment Stack\n",
    "        STR R6, R5, #0      ;; Save R6 to Stack\n",
    "        ADD R5, R5, #1      ;; Increment Stack\n",
    "        STR R7, R5, #0      ;; Save R7 to Stack\n",
    "        ADD R5, R5, #1      ;; Increment Stack\n",
    "        ST R5, Top\n",
    "        JMP R6\n",
    "        \n",
    "POP:    LD R5, Top\n",
    "        ADD R5, R5, #-1     ;; Decrement Stack\n",
    "        LDR R7, R5, #0      ;; Load stack into R7\n",
    "        ADD R5, R5, #-1     ;; Decrement Stack\n",
    "        LDR R6, R5, #0      ;; Load Stack into R6\n",
    "        ADD R5, R5, #-1     ;; Decrement Stack\n",
    "        LDR R4, R5, #0      ;; Load Stack into R4\n",
    "        ADD R5, R5, #-1     ;; Decrement Stack\n",
    "        LDR R3, R5, #0      ;; Load Stack into R3\n",
    "        ADD R5, R5, #-1     ;; Decrement Stack\n",
    "        LDR R2, R5, #0      ;; Load Stack into R2\n",
    "        ADD R5, R5, #-1     ;; Decrement Stack\n",
    "        LDR R1, R5, #0      ;; Load Stack into R1\n",
    "        ADD R5, R5, #-1     ;; Decrement Stack\n",
    "        LDR R0, R5, #0      ;; Load Stack into R0\n",
    "        ST R5, Top\n",
    "        RET\n",
    "\n",
    ";;-----------------------------------------------------------------\n",
    ";; Main loop variables \n",
    "LOC: .FILL ROOM_UINPUT\n",
    "NAME: .STRINGC \"Ruby\"\n",
    "STATS: .FILL #0 ;; Start of stats block\n",
    "HP: .FILL #0    ;; Stats + 1\n",
    "CHA: .FILL #0   ;; Stats + 2\n",
    "VIT: .FILL #0   ;; Stats + 3\n",
    "WIS: .FILL #0   ;; Stats + 4\n",
    "\n",
    "STAT_NAMES: .FILL #0 ;; Start of stat names block\n",
    "HP_NAMEREF: .FILL HP_NAME\n",
    "CHA_NAMEREF: .FILL CHA_NAME\n",
    "VIT_NAMEREF: .FILL VIT_NAME\n",
    "WIS_NAMEREF: .FILL WIS_NAME\n",
    "\n",
    "HP_NAME: .STRINGC \"time remaining\"\n",
    "CHA_NAME: .STRINGC \"charisma\"\n",
    "VIT_NAME: .STRINGC \"vitality\"\n",
    "WIS_NAME: .STRINGC \"wisdom\"\n",
    "\n",
    ";;STAT_T1: .STRINGC \"Your \"\n",
    ";;STAT_T2: .STRINGC \" increased!\\n\"\n",
    ";;STAT_T3: .STRINGC \" decreased!\\n\"\n",
    "\n",
    ";;-----------------------------------------------------------------\n",
    "\n",
    "STATS_ADDR: .FILL STATS\n",
    "STATNAMES_ADDR: .FILL STAT_NAMES\n",
    "\n",
    ";; Main game loop\n",
    "LOOP_START: \n",
    "\n",
    "    LD R1, LOC     ;; Load current state's address into R1 so we can access variables relative to it\n",
    "\n",
    "    ;; Perform stat increase for room, if it has one\n",
    "    \n",
    "    LDR R0, R1, #0 ;; Load which_quality into R0\n",
    "    BRz NO_CHANGE  ;; If which_quality is zero, skip stat change\n",
    "    \n",
    "    LD R5, STATS_ADDR ;; Load the address of the stat array into R5\n",
    "    ADD R2, R0, R5 ;; Add the offset in R0 to the address in R5 to get the address of the appropriate stat\n",
    "    LDR R3, R2, #0 ;; Load the current value of that stat into R3\n",
    "    LDR R4, R1, #1 ;; Load R4 with the amount the stat will be changed\n",
    "    ADD R3, R3, R4 \n",
    "    STR R3, R2, #0 ;; Store modified stat back into memory\n",
    "    \n",
    "    ;; Print message about stat increase\n",
    "    \n",
    "    ;;LEA R0, STAT_T1\n",
    "    ;;PUTSP\n",
    "    \n",
    "    LDR R2, R1, #0\n",
    "    LEA R3, STAT_NAMES\n",
    "    ADD R3, R3, R2\n",
    "    LDR R0, R3, #0\n",
    "    PUTSP\n",
    "\n",
    "    ;;LEA R0, STAT_T2\n",
    "    ;;PUTSP\n",
    "    ;;LEA R0, STAT_T3\n",
    "    ;;PUTSP\n",
    "\n",
    "    ;; Todo: Skip to endgame if your HP is 0\n",
    "    ;; Todo: Print name and HP - only if uinput? \n",
    "    \n",
    "NO_CHANGE:\n",
    "    LDR R2, R1, #2 ;; Load room_type into R2\n",
    "    ADD R2, R2, #-1 \n",
    "    BRn UINPUT     ;; Break to code for appropriate room type\n",
    "    BRz DICE\n",
    "    ;;BRp VARIABLE   \n",
    "\n",
    "LOOP_DONE: \n",
    "    BRnzp LOOP_START   ;; Repeat main game loop\n",
    "    HALT               ;; Right now this halt is never reached bc it loops forever LMAO\n",
    "\n",
    "UINPUT:            ;; For user-directed rooms, print description, then use subroutine that reads & parses number from 1-3\n",
    "    ADD R0, R1, #6 ;; Load R0 with address of room's description\n",
    "    PUTSP          ;; Print room description\n",
    "    \n",
    "    LEA R6, CHOICE\n",
    "    JSR PUSH\n",
    "    BRnzp LOOP_DONE\n",
    "\n",
    "DICE: \n",
    "    ;; Get a roll using D16\n",
    "    LEA R6, D16\n",
    "    JSR PUSH\n",
    "    \n",
    "    ;; Now there is a number between 1 and 16 in D16_roll\n",
    "    LD R0, D16_roll\n",
    "    \n",
    "    ;; Load which_stat into R2\n",
    "    LDR R2, R1, #3\n",
    "    \n",
    "    ;; Get the value of that stat\n",
    "    LEA R3, STATS\n",
    "    ADD R3, R3, R2\n",
    "    LDR R3, R3, #0\n",
    "    \n",
    "    ;; Add it to the dice roll to get [result] + [stat bonus]\n",
    "    ADD R0, R0, R3\n",
    "    \n",
    "    ;; Load difficulty check value into R2\n",
    "    LDR R2, R1, #4\n",
    "    \n",
    "    ;; Negate R2 for comparison\n",
    "    NOT R2, R2\n",
    "    ADD R2, R2, #1\n",
    "    \n",
    "    ;; Add R2 to R0 to compare difficulty check to total roll\n",
    "    ADD R2, R2, R0\n",
    "    \n",
    "    ;; Now R2 is negative if failure or zero/positive for success\n",
    "    BRn FAILURE\n",
    "    \n",
    "    ;; If success, load address of success location into LOC\n",
    "    LD R2, LOC\n",
    "    ADD R2, R2, #5\n",
    "    BRnzp DICE_DONE\n",
    "\n",
    "FAILURE: \n",
    "    LD R2, LOC\n",
    "    ADD R2, R2, #6\n",
    "\n",
    "DICE_DONE: \n",
    "    LDR R3, R2, #0 ;; Load new location at address R2 into R3\n",
    "    ST R3, LOC     ;; Store new location in LOC \n",
    "    BRnzp LOOP_DONE\n",
    "\n",
    ";; Code for variable rooms....... coming soon???\n",
    "\n",
    ";; ------------------------------------------------------------------\n",
    ";; Subroutine to get a number from 1-3 from the user\n",
    ";; Copied from Anita's code in printing_text\n",
    "\n",
    "CHOICE:   \n",
    "          IN\n",
    "          LD R2, LOC\n",
    "          LD R1, ASCII     ;; Load inverted ASCII conversion constant into R1\n",
    "          ADD R0, R0, R1   ;; Add it to convert ASCII value into a number\n",
    "          ADD R0, R0, #-2\n",
    "          BRn CH1 ;; user chose 1\n",
    "          BRz CH2 ;; user chose 2\n",
    "          BRp CH3 ;; user chose 3\n",
    "          \n",
    "CH1:      ADD R2, R2, #3\n",
    "          BRnzp CH_DONE\n",
    "CH2:      ADD R2, R2, #4\n",
    "          BRnzp CH_DONE\n",
    "CH3:      ADD R2, R2, #5\n",
    "          BRnzp CH_DONE\n",
    "          \n",
    "CH_DONE:  LDR R3, R2, #0 ;; Load the data at R2, aka the address of the #[choice] room, into R3\n",
    "          ST LOC, R3     ;; Store that data in LOC, moving player there \n",
    "          LEA R0, POP\n",
    "          JMP R0\n",
    "\n",
    ";; Data\n",
    "ASCII:   .FILL x-30\n",
    "\n",
    ";;; -----------------------------------------------------\n",
    ";;; After this runs, result in D16_roll\n",
    "D16: \n",
    "    ;; Call Random, loading a new random number into X\n",
    "    LEA R6, Random\n",
    "    JSR Push\n",
    "    \n",
    "    LD R0, X\n",
    "    AND R0, R0, #15\n",
    "    ADD R0, R0, #1\n",
    "    ST R0, D16_roll\n",
    "    \n",
    "    LEA R0, POP\n",
    "    JMP R0\n",
    "D16_roll: .FILL #0\n",
    "\n",
    "\n",
    ";;; -----------------------------------------------------\n",
    ";;; Memory X has next random number\n",
    "Random: \n",
    "    LD R0, M\n",
    "    LD R1, A\n",
    "    \n",
    "    LEA R6, Divide\n",
    "    JSR PUSH         ; R0 / R1\n",
    "    \n",
    "    ;; q = m / a\n",
    "    LD R0, QUOTIENT     ; R0 / R1\n",
    "    ST R0, Q \n",
    "    ;; r = m mod a\n",
    "    LD R0, REMAINDER    ; R0 mod R1\n",
    "    ST R0, R\n",
    "        ;; x / q\n",
    "    LD R0, X\n",
    "    LD R1, Q\n",
    "    \n",
    "    LEA R6, Divide\n",
    "    JSR PUSH        ; R0 / R1\n",
    "    \n",
    "    LD R1, QUOTIENT\n",
    "    ST R1, TEMP2\n",
    "    LD R1, REMAINDER    ; x mod q\n",
    "    ST R1, TEMP1\n",
    "    ;;      a * TEMP1 - r * TEMP2\n",
    "    LD R0, A\n",
    "    \n",
    "    LEA R6, Multiply\n",
    "    JSR PUSH        \n",
    "    LD R2, MULT_PRODUCT  ; R2 <- R0 * R1\n",
    "    \n",
    "    ST R2, TEMP1\n",
    "    ;;      a * TEMP1 - r * TEMP2\n",
    "    LD R0, R\n",
    "    LD R1, TEMP2\n",
    "    \n",
    "    LEA R6, Multiply\n",
    "    JSR PUSH            \n",
    "    LD R2, MULT_PRODUCT ; R2 <- r * TEMP2\n",
    "    \n",
    "    NOT R2,R2           ; -R2\n",
    "    ADD R2,R2,#1\n",
    "    ST R2, TEMP2 \n",
    "    LD R1, TEMP1\n",
    "    ADD R2, R2, R1      ; TEMP1 - TEMP2\n",
    "    \n",
    "TEST:  BRzp DONE        ; if x < 0 then\n",
    "    LD R1, M\n",
    "    ADD R2, R2, R1      ; \n",
    "DONE: ST R2, X\n",
    "    LEA R0, POP         ;; Return\n",
    "    JMP R0\n",
    "\n",
    "A: .FILL #7             ;; a , the multiplicative constant is given\n",
    "M: .FILL #32767         ;; m = 2 Ë† 15 âˆ’ 1, the modulus is given\n",
    "X: .FILL #10            ;; x, the seed is given\n",
    "R: .FILL #0\n",
    "Q: .FILL #0\n",
    "TEMP1: .FILL #0\n",
    "TEMP2: .FILL #0\n",
    "BACK: .FILL #0\n",
    "\n",
    ";;; -----------------------------------------------------\n",
    ";;; Mult_Product <- R0 * R1\n",
    ";;; Also uses R3 to store SIGN\n",
    "Multiply: AND R2,R2,#0\n",
    "  AND R3,R3,#0\n",
    "  ADD R0,R0,#0         ; compare R0\n",
    "  BRn MultNEG1\n",
    "  BR  MultCont\n",
    "MultNEG1: NOT R3,R3         ; flip SIGN\n",
    "  NOT R0,R0\n",
    "  ADD R0,R0,#1\n",
    "MultCONT: ADD R1,R1,#0         ; compare R1\n",
    "  BRn MultNEG2\n",
    "  BR MultInit\n",
    "MultNEG2: NOT R3,R3         ; flip SIGN\n",
    "  NOT R1,R1\n",
    "  ADD R1,R1,#1\n",
    "MultInit: ADD R0,R0,#0      ; have R0 set the condition codes\n",
    "MultLoop: BRz MultDone\n",
    "  ADD R2,R2,R1\n",
    "  ADD R0,R0,#-1\n",
    "  BR MultLoop\n",
    "MultDone: ADD R0,R3,#0\n",
    "  BRzp MultRet\n",
    "  NOT R2,R2\n",
    "  ADD R2,R2,#1\n",
    "MultRet:\n",
    "  ST R2, MULT_PRODUCT           ; Product has the product\n",
    "  LEA R0, POP\n",
    "  JMP R0\n",
    "  \n",
    "MULT_PRODUCT: .FILL #0\n",
    "\n",
    ";;; -----------------------------------------------------\n",
    ";;; R0 / R1\n",
    ";;; Also uses R3 to store SIGN\n",
    ";;;           R4 to store -R1\n",
    ";;;           R5 is QUOTIENT\n",
    ";;;           R6 is REMAINDER\n",
    ";;;           R2 temp\n",
    "Divide:   AND R3,R3,#0\n",
    "  ST R3, QUOTIENT\n",
    "  ST R3, REMAINDER\n",
    "  ADD R0,R0,#0         ; compare R0\n",
    "  BRn DivNEG1\n",
    "  BR  DivCont\n",
    "DivNEG1:  NOT R3,R3         ; flip SIGN\n",
    "  NOT R0,R0\n",
    "  ADD R0,R0,#1\n",
    "DivCONT:  ADD R1,R1,#0         ; compare R1\n",
    "  BRn DivNEG2\n",
    "  BR DivInit\n",
    "DivNEG2:  NOT R3,R3         ; flip SIGN\n",
    "  NOT R1,R1\n",
    "  ADD R1,R1,#1\n",
    "DivInit:  ADD R4,R1,#0\n",
    "  NOT R4,R4\n",
    "  ADD R4,R4,#1\n",
    "DivLoop:  ADD R2,R0,R4      ; have R2 set the condition codes\n",
    "  BRn DivDone\n",
    "  ADD R0,R0,R4\n",
    "  LD R2,QUOTIENT\n",
    "  ADD R2,R2,#1\n",
    "  ST R2,QUOTIENT\n",
    "  BR DivLoop\n",
    "DivDone:  ADD R3,R3,#0         ; Negative?\n",
    "  BRzp DivRet\n",
    "  LD R2,QUOTIENT     ; Yes, then negate R2\n",
    "  NOT R2,R2\n",
    "  ADD R2,R2,#1\n",
    "  ST R2,QUOTIENT\n",
    "DivRet:      ST R0,REMAINDER\n",
    "  LEA R0, POP\n",
    "  JMP R0\n",
    "QUOTIENT: .FILL #0\n",
    "REMAINDER: .FILL #0\n",
    "\n",
    ";; ------------------------------------------------------------------\n",
    ";; Rooms data\n",
    "ROOM_UINPUT: \n",
    "    .FILL #0 ;; Which quality (stat/HP) it changes: None\n",
    "    .FILL #0 ;; Amount of change: None\n",
    "    .FILL #0 ;; 0: Based on user input\n",
    "    .FILL ROOM_UINPUT\n",
    "    .FILL ROOM_RANDOM\n",
    "    .FILL ROOM_UINPUT ;; Three choices all lead back here\n",
    "    .STRINGC \"Here we are in an example room. Enter 1 to jump in place, 2 to go to the random room, or 3 to spin in a circle\\n\"\n",
    "    \n",
    "ROOM_RANDOM: \n",
    "    .FILL #1 ;; Quality changed: HP\n",
    "    .FILL #1 ;; Amount of change: 1\n",
    "    .FILL #1 ;; 1: Based on dice roll\n",
    "    .FILL #3 ;; Stat for dice roll: Vitality\n",
    "    .FILL #8 ;; Difficulty of check\n",
    "    .FILL ROOM_UINPUT ;; Destination if success\n",
    "    .FILL ROOM_RANDOM ;; Destination if failure\n",
    "    \n",
    "ROOM_VARIABLE:\n",
    "    .FILL #0 ;; Quality changed: None\n",
    "    .FILL #0 ;; Amount of change: None\n",
    "    .FILL #2 ;; 2: Based on variable\n",
    "    .FILL #0 ;; VAR_ADDRESS ;; What variable to check\n",
    "    .FILL #0 ;; VALUE       ;; What value counts as a success\n",
    "    .FILL ROOM_VARIABLE ;; Destination if success\n",
    "    .FILL ROOM_VARIABLE ;; Destination if failure\n",
    "    \n",
    ".END"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Here we are in an example room. Enter 1 to jump in place, 2 to go to the random room, or 3 to spin in a circle\n",
      "\n",
      "Input a character> "
     ]
    }
   ],
   "source": [
    "%exe"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Calysto LC3",
   "language": "asm",
   "name": "calysto_lc3"
  },
  "language_info": {
   "file_extension": ".asm",
   "mimetype": "text/x-gas",
   "name": "gas"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
